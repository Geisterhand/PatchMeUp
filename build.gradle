buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://oss.sonatype.org/content/groups/public' }
        maven { url "https://plugins.gradle.org/m2/" }

    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
        classpath "io.franzbecker:gradle-lombok:1.14"
    }
}

plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "io.franzbecker.gradle-lombok"
apply plugin: "com.github.johnrengelman.shadow"

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: modMcpMappingsChannel, version: modMcpMappingsVersion

    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

version = modMinecraftVersion + "-" + modVersion
group = modGroup
archivesBaseName = modFileName

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    ivy {
        url "https://download.nodecdn.net/containers/reforged/server/release"
        patternLayout {
            artifact "[revision]/[artifact].[ext]"
        }
        metadataSources {
            artifact()
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:" + modMinecraftVersion + "-" + modForgeVersion
    shadow 'org.spongepowered:configurate-yaml:4.1.2'
    implementation ":Pixelmon-1.12.2-8.3.4-server:8.3.4"
}

shadowJar {
    configurations = Arrays.asList(project.configurations.shadow)
    relocate('org.spongepowered.configurate', 'eu.pixelunited.patchmeup.dependencies.configurate') {
        exclude 'META-INF', 'META-INF/**'
    }
    relocate 'org.yaml.snakeyaml', 'eu.pixelunited.patchmeup.dependencies.snakeyaml'

    archiveClassifier.set("")
    minimize()
}

reobf {
    shadowJar {}
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : modId,
                "Specification-Vendor"    : modGroup,
                "Specification-Version"   : "1.0", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${archiveVersion}",
                "Implementation-Vendor"   : modGroup,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources{
    // This will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version

    // Replace stuff in mods.toml, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mods.toml'

        // Replace version
        expand 'version': project.version
    }

    // Copy everything else except the mods.toml
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mods.toml'
    }
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
}
